<START> -> <PROG> 'endoffile'

<ADDOP> -> 'plus'
<ADDOP> -> 'minus'
<ADDOP> -> 'or'

<APARAMS> -> <EXPR> <REPTAPARAMS1>
<APARAMS> -> EPSILON

<APARAMSTAIL> -> 'comma' <EXPR>

<ARITHEXPR> -> <TERM> <RIGHTRECARITHEXPR>

<ARRAYSIZE> -> 'opensqbr' <ARRAYSIZE2>

<ARRAYSIZE2> -> 'intnum' 'closesqbr'
<ARRAYSIZE2> -> 'closesqbr'

<EXPR> -> <ARITHEXPR> <EXPR2>

<EXPR2> -> <RELOP> <ARITHEXPR>
<EXPR2> -> EPSILON

<FACTOR> -> 'id' <FACTOR2> <REPTVARIABLEORFUNCTIONCALL>
<FACTOR> -> 'intnum' !makeInt
<FACTOR> -> 'floatnum'
<FACTOR> -> 'openpar' <ARITHEXPR> 'closepar'
<FACTOR> -> 'not' <FACTOR>
<FACTOR> -> <SIGN> <FACTOR>

<FACTOR2> -> 'openpar' <APARAMS> 'closepar'
<FACTOR2> -> <REPTIDNEST1>

<FPARAMS> -> 'id' 'colon' <TYPE> <REPTFPARAMS3> <REPTFPARAMS4>
<FPARAMS> -> EPSILON

<FPARAMSTAIL> -> 'comma' 'id' 'colon' <TYPE> <REPTFPARAMSTAIL4>

<FUNCBODY> -> 'opencurbr' <REPTLOCALVARORSTAT> 'closecurbr'

<MEMBERFUNCDECL> -> <MEMBERFUNCHEAD> 'semi'

<MEMBERFUNCHEAD> -> 'function' 'id' 'colon' 'openpar' <FPARAMS> 'closepar' 'arrow' <RETURNTYPE>
<MEMBERFUNCHEAD> -> 'constructorkeyword' 'colon' 'openpar' <FPARAMS> 'closepar'

<FUNCDEF> -> <FUNCHEAD> <FUNCBODY>

<FUNCHEAD> -> 'function' 'id' <FUNCHEADTAIL>
<FUNCHEADTAIL> -> 'sr' <FUNCHEADMEMBERTAIL>
<FUNCHEADTAIL> -> 'openpar' <FPARAMS> 'closepar' 'arrow' <RETURNTYPE>
<FUNCHEADMEMBERTAIL> -> 'id' 'openpar' <FPARAMS> 'closepar' 'arrow' <RETURNTYPE>
<FUNCHEADMEMBERTAIL> -> 'constructorkeyword' 'openpar' <FPARAMS> 'closepar'


<STATEMENT> -> 'id' !makeIdentifier <STATEMENTIDNEST> 'semi'
<STATEMENT> -> 'if' 'openpar' <RELEXPR> 'closepar' 'then' <STATBLOCK> 'else' <STATBLOCK> 'semi'
<STATEMENT> -> 'while' 'openpar' <RELEXPR> 'closepar' <STATBLOCK> 'semi'
<STATEMENT> -> 'read' 'openpar' <VARIABLE> 'closepar' 'semi'
<STATEMENT> -> 'write' 'openpar' <EXPR> 'closepar' 'semi'
<STATEMENT> -> 'return' 'openpar' <EXPR> 'closepar' 'semi'

<STATEMENTIDNEST> -> 'dot' 'id' <STATEMENTIDNEST>
<STATEMENTIDNEST> -> 'openpar' <APARAMS> 'closepar' <STATEMENTIDNEST2>
<STATEMENTIDNEST> -> <INDICE> <REPTIDNEST1> <STATEMENTIDNEST3>
<STATEMENTIDNEST> -> 'assign' <EXPR> !makeAssExpr

<STATEMENTIDNEST2> -> EPSILON
<STATEMENTIDNEST2> -> 'dot' 'id' <STATEMENTIDNEST>

<STATEMENTIDNEST3> -> 'assign' <EXPR>
<STATEMENTIDNEST3> -> 'dot' 'id' <STATEMENTIDNEST>

<REPTIDNEST1> -> <INDICE> <REPTIDNEST1>
<REPTIDNEST1> -> EPSILON

<REPTVARIABLEORFUNCTIONCALL> -> <IDNEST> <REPTVARIABLEORFUNCTIONCALL>
<REPTVARIABLEORFUNCTIONCALL> -> EPSILON

<IDNEST> -> 'dot' 'id' <IDNEST2>
<IDNEST2> -> 'openpar' <APARAMS> 'closepar'
<IDNEST2> -> <REPTIDNEST1>

<INDICE> -> 'opensqbr' <ARITHEXPR> 'closesqbr'

<MEMBERDECL> -> <MEMBERFUNCDECL>
<MEMBERDECL> -> <MEMBERVARDECL>

<MEMBERVARDECL> -> 'attribute' 'id' 'colon' <TYPE> <REPTARRAYSIZE> 'semi'

<MULTOP> -> 'mult'
<MULTOP> -> 'div'
<MULTOP> -> 'and'

<OPTINHERITS> -> 'isa' 'id' <REPTINHERITSLIST>
<OPTINHERITS> -> EPSILON

<REPTINHERITSLIST> -> 'comma' 'id' <REPTINHERITSLIST>
<REPTINHERITSLIST> -> EPSILON

<PROG> -> <REPTPROG0> !makeProgram

<RELEXPR> -> <ARITHEXPR> <RELOP> <ARITHEXPR>

<RELOP> -> 'eq'
<RELOP> -> 'neq'
<RELOP> -> 'lt'
<RELOP> -> 'gt'
<RELOP> -> 'leq'
<RELOP> -> 'geq'

<REPTAPARAMS1> -> <APARAMSTAIL> <REPTAPARAMS1>
<REPTAPARAMS1> -> EPSILON

<REPTFPARAMS3> -> <ARRAYSIZE> <REPTFPARAMS3>
<REPTFPARAMS3> -> EPSILON

<REPTFPARAMS4> -> <FPARAMSTAIL> <REPTFPARAMS4>
<REPTFPARAMS4> -> EPSILON

<REPTFPARAMSTAIL4> -> <ARRAYSIZE> <REPTFPARAMSTAIL4>
<REPTFPARAMSTAIL4> -> EPSILON

<REPTPROG0> -> <CLASSDECLORFUNCDEF> <REPTPROG0>
<REPTPROG0> -> EPSILON

<CLASSDECLORFUNCDEF> -> <CLASSDECL>
<CLASSDECLORFUNCDEF> -> <FUNCDEF>

<REPTSTATBLOCK1> -> <STATEMENT> <REPTSTATBLOCK1>
<REPTSTATBLOCK1> -> EPSILON

<REPTMEMBERDECL> -> <VISIBILITY> <MEMBERDECL> <REPTMEMBERDECL>
<REPTMEMBERDECL> -> EPSILON

<REPTARRAYSIZE> -> <ARRAYSIZE> <REPTARRAYSIZE>
<REPTARRAYSIZE> -> EPSILON

<RETURNTYPE> -> <TYPE>
<RETURNTYPE> -> 'void'

<RIGHTRECARITHEXPR> -> <ADDOP> <TERM> <RIGHTRECARITHEXPR>
<RIGHTRECARITHEXPR> -> EPSILON

<RIGHTRECTERM> -> <MULTOP> <FACTOR> <RIGHTRECTERM>
<RIGHTRECTERM> -> EPSILON

<SIGN> -> 'plus'
<SIGN> -> 'minus'

<STATBLOCK> -> 'opencurbr' <REPTSTATBLOCK1> 'closecurbr'
<STATBLOCK> -> <STATEMENT>
<STATBLOCK> -> EPSILON

<CLASSDECL> -> 'class' 'id' <OPTINHERITS> 'opencurbr' <REPTMEMBERDECL> 'closecurbr' 'semi'

<TERM> -> <FACTOR> <RIGHTRECTERM>

<TYPE> -> 'integer'
<TYPE> -> 'float'
<TYPE> -> 'id'

<LOCALVARDECL> -> 'localvar' 'id' 'colon' <TYPE> <ARRAYOROBJECT> 'semi'
<ARRAYOROBJECT> -> <REPTARRAYSIZE>
<ARRAYOROBJECT> -> 'openpar' <APARAMS> 'closepar'

<REPTLOCALVARORSTAT> -> <LOCALVARORSTAT> <REPTLOCALVARORSTAT>
<REPTLOCALVARORSTAT> -> EPSILON

<LOCALVARORSTAT> -> <LOCALVARDECL>
<LOCALVARORSTAT> -> <STATEMENT>

<VARIABLE> -> 'id' <VARIABLE2>

<VARIABLE2> -> <REPTIDNEST1> <REPTVARIABLE>
<VARIABLE2> -> 'openpar' <APARAMS> 'closepar' <VARIDNEST>

<REPTVARIABLE> -> <VARIDNEST> <REPTVARIABLE>
<REPTVARIABLE> -> EPSILON

<VARIDNEST> -> 'dot' 'id' <VARIDNEST2>
<VARIDNEST2> -> 'openpar' <APARAMS> 'closepar' <VARIDNEST>
<VARIDNEST2> -> <REPTIDNEST1>

<VISIBILITY> -> 'public'
<VISIBILITY> -> 'private'